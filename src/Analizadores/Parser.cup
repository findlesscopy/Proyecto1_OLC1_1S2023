package Analizadores;
import java_cup.runtime.*;
import java.util.ArrayList;
import Arbol.Nodo;
import Arbol.Arbol;

//PARSER
parser code
{://Codigo visible
    public ArrayList<Arbol> listadoExpresiones = new ArrayList<>();

    public void syntax_error(Symbol s){
        System.out.println("Error R de sintaxis: "+ s.value +" Linea "+(s.left+1)+" columna "+(s.right+1) );
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        System.out.println("Error NR de sintaxis: "+ s.value +" Linea "+(s.left+1)+" columna "+(s.right+1) );
    }

:}
// COMENTARIOS Y ESPACIOS IGNORADOS

//PALABRAS RESERVADAS

terminal CONJ, LL_ABRE, LL_CIERRA, D_PUNTOS, P_COMA, COMA, GUION, MAYOR, VIRGULILLA, PORCENTAJE;
terminal OR, CONCAT, POSITIVA, KLEENE, OPCIONAL;
terminal L_MINUSCULA, L_MAYUSCULA, DIGITO, ESPECIAL, ID, CARACTER, CADENA;

non terminal olc;
non terminal inicio;
non terminal definiciones, definicion, rango, expresion_regular, listado, unico;
non terminal pruebas, prueba;

start with olc;

olc::= LL_ABRE definiciones PORCENTAJE PORCENTAJE PORCENTAJE PORCENTAJE pruebas LL_CIERRA;

definiciones::= definiciones definicion
               | definicion;

definicion::= CONJ D_PUNTOS ID GUION MAYOR rango P_COMA
             |ID GUION MAYOR expresion_regular:a P_COMA {: listadoExpresiones.add(new Arbol((Nodo)a)); :};

rango::= L_MINUSCULA VIRGULILLA L_MINUSCULA
        | L_MAYUSCULA VIRGULILLA L_MAYUSCULA
        | DIGITO VIRGULILLA DIGITO
        | ESPECIAL VIRGULILLA ESPECIAL
        | listado;

listado::= listado COMA unico
          | unico;

unico::= L_MINUSCULA
        | L_MAYUSCULA
        | DIGITO
        | ESPECIAL;

expresion_regular::= CONCAT:a expresion_regular:b expresion_regular:c {: Nodo padre = new Nodo((String)a); padre.setIzquierda((Nodo)b); padre.setDerecha((Nodo)c); RESULT = padre; :}
                    | OR:a expresion_regular:b expresion_regular:c {: Nodo padre = new Nodo((String)a); padre.setIzquierda((Nodo)b); padre.setDerecha((Nodo)c); RESULT = padre; :}
                    | POSITIVA:a expresion_regular:b {: Nodo padre = new Nodo((String)a); padre.setIzquierda((Nodo)b); RESULT = padre; :}
                    | KLEENE:a expresion_regular:b {: Nodo padre = new Nodo((String)a); padre.setIzquierda((Nodo)b); RESULT = padre; :}
                    | OPCIONAL:a expresion_regular:b {: Nodo padre = new Nodo((String)a); padre.setIzquierda((Nodo)b); RESULT = padre; :}
                    | CARACTER:a {: Nodo leave = new Nodo((String)a); leave.setHoja(true) ;RESULT = leave; :}
                    | LL_ABRE ID:a LL_CIERRA {: Nodo leave = new Nodo((String)a); leave.setHoja(true); RESULT = leave; :};

pruebas ::= pruebas prueba
           | prueba;

prueba::= ID D_PUNTOS CADENA P_COMA;
